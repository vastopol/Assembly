
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit consoleUnit;

#include( "console.hhf" )
#includeonce( "os.hhf" )
#includeonce( "stdout.hhf" )

?@nodisplay := true;
?@noalignstack := true;	

#if( os.linux | os.freeBSD | os.macOS )


	// scrollUp - Scrolls the entire screen up one line.
	//
	// Linux:
	// ANSI ESC sequence is ESC "[r" ESC "M"

	procedure console.scrollUp; @noframe;
	readonly
		scrollUpStr:byte; @nostorage;
				byte #$1b "[r" #$1b "M";
				
	begin scrollUp;
	
		push( eax );
		push( ebx );
		push( ecx );
		push( edx );

		stdout.write( &scrollUpStr, 5 );
		
		pop( edx );
		pop( ecx );
		pop( ebx );
		pop( eax );
		ret();
		
	end scrollUp;

#elseif( os.win32 )

	#include( "stdout.hhf" )
	
	type 
	 	COORD: record
			x: word;
			y: word;
		endrecord;


	 	SMALL_RECT: record
			Left: word;
			Top: word;
			Right: word;
			Bottom: word;
		endrecord;
	
	 	CHARTYPE: union
			UnicodeChar: word;
			AsciiChar: byte;
		endunion;

	 	CHAR_INFO: record
			CharVal: CHARTYPE;
			Attributes: word;
		endrecord;

		CONSOLE_SCREEN_BUFFER_INFO: record

			dwSize: COORD;
			dwCursorPosition: COORD;
			wAttributes: word;
			srWindow: SMALL_RECT;
			dwMaximumWindowSize: COORD;
			
		endrecord;
		
	static
		FillConsoleOutputAttribute: procedure
		( 
				hConsoleOutput			:dword; 
				wAttribute				:word; 
				nLength					:dword; 
				dwWriteCoord			:COORD; 
			var	lpNumberOfAttrsWritten	:dword
		);
			@stdcall;
			@returns( "eax" );
			@external( "__imp__FillConsoleOutputAttribute@20" );

		FillConsoleOutputCharacter: procedure
		( 
			hConsoleOutput:				dword; 
			cCharacter:					char; 
			nLength:					dword; 
			dwWriteCoord:				COORD; 
			var	lpNumberOfAttrsWritten:	dword
		);
			@stdcall;
			@returns( "eax" );
			@external( "__imp__FillConsoleOutputCharacterA@20" );

			
		GetConsoleScreenBufferInfo: procedure
		( 
				handle:	dword; 
			var csbi:	CONSOLE_SCREEN_BUFFER_INFO
		);
			@stdcall;
			@returns( "eax" );
			@external( "__imp__GetConsoleScreenBufferInfo@8" );
		
		ScrollConsoleScreenBuffer: procedure
		( 
				hConsoleOutput:			dword; 
			var	lpScrollRectangle:		SMALL_RECT; 
			var	lpClipRectangle:		SMALL_RECT; 
				dwDestinationOrigin:	COORD; 
			var	lpFill:					CHAR_INFO
		);
			@stdcall;
			@returns( "eax" );
			@external( "__imp__ScrollConsoleScreenBufferA@20" );
	

	
	
	procedure console.scrollUp; @nodisplay; @noframe;
	var
		handle			:dword;
		size			:dword;
		src				:SMALL_RECT;
		dest			:COORD;
		ci				:CHAR_INFO;
		csbi			:CONSOLE_SCREEN_BUFFER_INFO;
		
	begin scrollUp;

		push( ebp );
		mov( esp, ebp );
		sub( _vars_, esp );
		
		pushad();
		
		stdout.handle();
		mov( eax, handle );
		
		GetConsoleScreenBufferInfo( handle, csbi );

		// Compute the bounds of the rectangle to move.
		// And the destination coordinate to move it to.

		xor( eax, eax );
		mov( ax, dest.x );
		mov( ax, dest.y );
		mov( ax, src.Left );
		mov( csbi.dwSize.x, ax );
		mov( ax, src.Right );

		mov( 1, src.Top );
		mov( csbi.dwSize.y, ax );
		dec( ax );
		mov( ax, src.Bottom );


		// Set up the fill char and attribute:

		mov( ' ', ci.CharVal.AsciiChar );
		mov( csbi.wAttributes, ax );
		mov( ax, ci.Attributes );

		// Okay, scroll the screen up one line:

		ScrollConsoleScreenBuffer
		(
			handle,
			src,
			NULL,	// NULL clipping region.
			#{
				pushd( (type dword dest));
			}#,
			ci
		);

		popad();
		leave();
		ret();	

	end scrollUp;	

#endif
	
end consoleUnit;
