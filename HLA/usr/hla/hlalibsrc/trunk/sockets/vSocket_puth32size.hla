
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit socketUnit;
		
#includeOnce( "sockets.hhf" )
#includeOnce( "hla.hhf" )

/****************************************************************/
/*                                                              */
/* baseSocketClass_t.puth32Size-                                */
/*                                                              */
/* Outputs a 32-bit value as a hexadecimal string to some file. */
/*                                                              */
/* Inputs:                                                      */
/*                                                              */
/* 	Handle-		Handle of file to receive output.               */
/* 	d-			32-bit value to convert and output              */
/* 	width-		Minimum field width for output (max 4095)       */
/* 	fill-		Padding character                               */
/*                                                              */
/****************************************************************/


method vBaseSocketClass_t.puth32Size( d:dword; width:int32; fill:char ); 
	@noframe;
	@nodisplay;
	@noalignstack;

var
	eaxSave		:dword;
	ediSave		:dword;
	esiSave		:pointer to vBaseSocketClass_t;


	procedure vputh32sizeThunk( thunksEBPval:dword ); @noframe; @nodisplay;
	begin vputh32sizeThunk;
	
		push( ebp );			// Thunks always do this.
		mov( [esp+8], ebp );	// Retrieve EBP value passed as parameter
		
		mov( ebp::esiSave, esi );
		(type vBaseSocketClass_t [esi]).puts( (type string eax));
		
		pop( ebp );
		ret( _parms_ );
		
	end vputh32sizeThunk;
	
begin puth32Size;

	push( ebp );
	mov( esp, ebp );
	sub( _vars_, esp );
	mov( esi, esiSave );
	mov( edi, ediSave );
	mov( eax, eaxSave );
	
	hla.puth32size
	( 
		#{	// Push thunk object on stack: 
			pushd( &vputh32sizeThunk );
			push( ebp );
		}#, 
		d,
		width,
		fill 
	);

	mov( eaxSave, eax );
	mov( ediSave, edi );
	mov( esiSave, esi );
	leave();
	ret( _parms_ );
		
end puth32Size;

end socketUnit;

