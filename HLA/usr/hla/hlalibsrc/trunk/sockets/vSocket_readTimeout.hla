// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/7/2008, by Randall Hyde


unit sockets;
?@nostackalign := true;
?@noframe := true;
?@nodisplay := true;

#includeOnce( "sockets.hhf" )
#includeOnce( "excepts.hhf" )

method vBaseSocketClass_t.readTimeout( var buf:var; len:dword; msTimeout:dword );
var
	bytesRead	:dword;
	ebxSave		:dword;
	ecxSave		:dword;
	edxSave		:dword;
	esiSave		:dword;
	ediSave		:dword;
	timeout		:hla.timeval;
	fdset		:hla.fd_set_t;
	
	
begin readTimeout;

	push( ebp );
	mov( esp, ebp );
	sub( _vars_, esp );
	
	mov( edi, ediSave );	// We have to preserve the
	mov( esi, esiSave );	// register because we have
	mov( edx, edxSave );	// no idea what will happen to
	mov( ecx, ecxSave );	// them if there is an exception.
	mov( ebx, ebxSave );
	
	
	// Convert milliseconds to seconds and microseconds:
	
	mov( msTimeout, eax );
	mul( 1000, eax );			// Convert to microseconds
	div( 1_000_000, edx:eax );	// Secs->EAX, microseconds->EDX
	mov( eax, timeout.tv_sec );
	mov( edx, timeout.tv_usec );
	
	sock.fd_zero( fdset );
	sock.fd_set( this.handle, fdset );
	lea( eax, timeout );
	readMore:
	
		sock.select( this.handle, fdset, NULL, NULL, [eax] );
		test( eax, eax );	// Zero if timeout occurred.
		jz EndOfFile;		// Just return # chars read.
			
			sock.recv( this.handle, val buf, len, 0 );
			test( eax, eax );	// Zero if timeout occurred
			jz EndOfFile;
			
			add( eax, buf );
			add( eax, bytesRead );
			sub( eax, len );
			jnz readMore;

	EndOfFile:
	mov( ediSave, edi );
	mov( esiSave, esi );
	mov( edxSave, edx );
	mov( ecxSave, ecx );
	mov( ebxSave, ebx );
	leave();
	ret( _parms_ );
	
end readTimeout;


end sockets;
