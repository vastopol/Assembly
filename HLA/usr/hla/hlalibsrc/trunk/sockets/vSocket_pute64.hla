
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit socketUnit;
		
#includeOnce( "sockets.hhf" )
#includeOnce( "hla.hhf" )



/*********************************************************/
/*                                                       */
/* baseSocketClass_t.pute64-                             */
/*                                                       */
/* Writes a 64-bit floating-point value to the specified */
/* socket using scientific format.                       */
/*                                                       */
/* Inputs-                                               */
/* 	e64		:64-bit value to print.                      */
/* 	width	:# of print positions to use.                */
/* 	                                                     */
/*********************************************************/

method vBaseSocketClass_t.pute64
( 
	r		:real64; 
	width	:uns32 
);  
	@noframe;
	@nodisplay;
	@noalignstack;
var
	eaxSave		:dword;
	ediSave		:dword;
	esiSave		:pointer to vBaseSocketClass_t;


	procedure vpute64Thunk( thunksEBPval:dword ); @noframe; @nodisplay;
	begin vpute64Thunk;
	
		push( ebp );			// Thunks always do this.
		mov( [esp+8], ebp );	// Retrieve EBP value passed as parameter
		
		mov( ebp::esiSave, esi );
		(type vBaseSocketClass_t [esi]).puts( (type string eax));
		
		pop( ebp );
		ret( _parms_ );
		
	end vpute64Thunk;
	
begin pute64;

	push( ebp );
	mov( esp, ebp );
	sub( _vars_, esp );
	mov( esi, esiSave );
	mov( edi, ediSave );
	mov( eax, eaxSave );
	
	hla.pute64
	( 
		#{	// Push thunk object on stack: 
			pushd( &vpute64Thunk );
			push( ebp );
		}#, 
		r,
		width 
	);

	mov( eaxSave, eax );
	mov( ediSave, edi );
	mov( esiSave, esi );
	leave();
	ret( _parms_ );
	
end pute64;



end socketUnit;

