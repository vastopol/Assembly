
// I, Randall Hyde, hereby agree to waive all claim of copyright (economic
// and moral) in all content contributed by me, the user, and immediately
// place any and all contributions by me into the public domain; I grant
// anyone the right to use my work for any purpose, without any
// conditions, to be changed or destroyed in any manner whatsoever
// without any attribution or notice to the creator.  I also absolve myself
// of any responsibility for the use of this code, the user assumes all
// responsibilities for using this software in an appropriate manner.
//
// Notice of declaration of public domain, 7/12/2006, by Randall Hyde



unit blobClass;
?@NoStackAlign := true;
?@NoDisplay := true;
?@NoFrame := true;		

#includeOnce( "excepts.hhf" )
#includeOnce( "hla.hhf" )
#includeOnce( "blobs.hhf" )
#includeOnce( "threads.hhf" );


// blob.getTbyte-                                         
//                                               
// Reads 10 bytes from the blob.
// If successful (not EOF), then stores data in 'tb' parameter. 
// Returns Carry = 1 if EOF, carry = 0 if not EOF.



procedure blob.getTbyte( b:blob.blob; var tb:tbyte ); 
begin getTbyte;

	push( ebp );
	mov( esp, ebp );
	push( eax );
	push( ebx );
	push( ecx );
	
	mov( b, ebx );
	test( ebx, ebx );
	jz AttemptToDerefNULL;
	
	
	#if( @thread )
	
	try
	
		thread.enterCriticalSection( (type blob.blobRec [ebx]).criticalSection );
	
	#endif
	
		// Check to see if reading ten bytes would cause us to
		// read beyond the end of the blob.
		//
		// Note: jae = jnc.  Must use jae, not ja here so that we
		// leave carry clear if there is an EOF error (note that
		// we complement the carry flag later on).
		
		mov( (type blob.blobRec [ebx]).rcursor, eax );
		add( 9, eax );
		cmp( eax, (type blob.blobRec [ebx]).length );
		jae EOFerror;
			
			
			// Okay, there are at least ten bytes left at the end of
			// the blob. Update rcursor to be the original rcursor
			// value plus ten.
			
			add( 1, eax );		// make +10
			mov( eax, (type blob.blobRec [ebx]).rcursor );
			
			// Return the word at the original rcursor position:
			
			lea( ebx, [ebx+eax-10] );
			mov( tb, ecx );
			mov( [ebx], eax );
			mov( eax, [ecx] );
			mov( [ebx+4], eax );
			mov( eax, [ecx+4] );
			mov( [ebx+8], ax );
			mov( ax, [ecx+8] );
			
			// Return carry clear to specify "not EOF" (note CMC below).
			
			stc();
		
		EOFerror:
		cmc();
	
	#if( @thread )
	
	  anyexception
	
		mov( b, edx );
		thread.leaveCriticalSection( (type blob.blobRec [edx]).criticalSection );
		reraise( eax );
		
	endtry;
	pushfd();
	mov( b, ebx );
	thread.leaveCriticalSection( (type blob.blobRec [ebx]).criticalSection );
	popfd();
		
	#endif

	pop( ecx );
	pop( ebx );
	pop( eax );
	leave();
	ret( _parms_ );

AttemptToDerefNULL:
	raise( ex.AttemptToDerefNULL );
				
end getTbyte;




end blobClass;
